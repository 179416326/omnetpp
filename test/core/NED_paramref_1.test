%description:
 - reference to:
     parent module parameter,
     local submodule parameter,
     other submodule's parameter
   from
     module parameter assigment,
     gatesize assignment,
     module vector size expression,
     connections part ("for" start/end, module or gate index, etc)
     connection parameter value

FIXME TODO also with module vectors
FIXME TODO also within connections


%file: test.ned

import testlib.Dump;

module Node
{
    parameters:
        int p;
        int a = default(111);
        int b = default(222);
        int c = default(333);
    gates:
        input ga[];
        input gb[];
        input gc[];
    connections allowunconnected:
}

module Nil
{
}

network Test
{
    parameters:
        int p = 1;
    submodules:
        n: Node {
            parameters:
                p = 2;
        }
        m: Node {
            parameters:
                p = 3;
                // from parameter assignment:
                a = p;      // parent module's parameter
                b = this.p; // local submodule's parameter
                c = n.p;    // other submodule's parameter
            gates:
                // from gate vector size:
                ga[p];      // parent module's parameter
                gb[this.p]; // local submodule's parameter
                gc[n.p];    // other submodule's parameter
        };

        // from module vector size:
        ma[p]: Nil;      // parent module's parameter
        mb[this.p]: Nil; // local submodule's parameter
        mc[n.p]: Nil;    // other submodule's parameter

        // from ini file
        ini: Node;

        dump: Dump;

    connections allowunconnected:
        //TODO from module/gate index, from "for" start/end value, from "if" condition etc
}

%file: test.cc

%inifile: test.ini
[General]
network = Test
cmdenv-express-mode = false
cmdenv-event-banners = false

*.ini.p = 4
*.ini.a = p      # parent module's parameter
*.ini.b = this.p # local submodule's parameter
*.ini.c = n.p    # other submodule's parameter

**.apply-default = true

%contains: stdout
module Test: Test {
    parameters:
        @isNetwork
        p = 1
    submodules:
        module Test.n: Node {
            parameters:
                p = 2
                a = 111
                b = 222
                c = 333
        }
        module Test.m: Node {
            parameters:
                p = 3
                a = 1
                b = 3
                c = 2
            gates:
                 0 ga[0]: not connected
                 1 gb[0]: not connected
                 2 gb[1]: not connected
                 3 gb[2]: not connected
                 4 gc[0]: not connected
                 5 gc[1]: not connected
        }
        module Test.ma[0]: Nil {
        }
        module Test.mb[0]: Nil {
        }
        module Test.mc[0]: Nil {
        }
        module Test.mc[1]: Nil {
        }
}

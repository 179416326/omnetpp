\appendixchapter{Message Definitions Grammar}
\label{cha:msg-language-grammar}

This appendix contains the grammar for the message definitions
language.

In the language, space, horizontal tab and new line characters count
as delimiters, so one or more of them is required between two elements of the
description which would otherwise be unseparable.

'//' (two slashes) may be used to write comments that last to the end of the line.

The language is fully case sensitive.

Notation:
\begin{itemize}
  \item{rule syntax is that of \textit{bison}/\textit{yacc}}
  \item{uppercase words are terminals, lowercase words are nonterminals}
  \item{\ttt{NAME}, \ttt{CHARCONSTANT}, \ttt{STRINGCONSTANT}, \ttt{INTCONSTANT},
        \ttt{REALCONSTANT} represent identifier names and string, character,
        integer and real number literals (defined as in the C language)}
  \item{other terminals represent keywords in all lowercase}
\end{itemize}

Nonterminals ending in \ttt{\_old} are present so that message files
from the previous versions of {\opp} (3.x) can be parsed.

%
% Note: the following has been extracted directly from msg2.y,
% using tools/stripgrammar.
%
% TBD explain notation
%

\begin{Verbatim}[commandchars=\\\{\}]
msgfile
        : definitions
        ;

definitions
        : definitions definition
        |
        ;

definition
        : namespace\_decl
        | cplusplus
        | struct\_decl
        | class\_decl
        | message\_decl
        | packet\_decl
        | enum\_decl
        | enum
        | message
        | packet
        | class
        | struct
        ;

namespace\_decl
        : NAMESPACE namespacename ';'

namespacename
        : namespacename ':' ':' NAME
        | NAME
        ;

cplusplus
        : CPLUSPLUS '\{\{' ... '\}\}' opt\_semicolon
        ;

struct\_decl
        : STRUCT NAME ';'
        ;

class\_decl
        : CLASS NAME ';'
        | CLASS NONCOBJECT NAME ';'
        | CLASS NAME EXTENDS NAME ';'
        ;

message\_decl
        : MESSAGE NAME ';'
        ;

packet\_decl
        : PACKET NAME ';'
        ;

enum\_decl
        : ENUM NAME ';'
        ;

enum
        : ENUM NAME '\{'
          opt\_enumfields '\}' opt\_semicolon
        | ENUM NAME EXTENDS NAME '\{'
          opt\_enumfields '\}' opt\_semicolon
        ;

opt\_enumfields
        : enumfields
        |
        ;

enumfields
        : enumfields enumfield
        | enumfield
        ;

enumfield
        : NAME ';'
        | NAME '=' enumvalue ';'
        ;

message
        : message\_header body
        ;

packet
        : packet\_header body
        ;

class
        : class\_header body
        ;

struct
        : struct\_header body
        ;

message\_header
        : MESSAGE NAME '\{'
        | MESSAGE NAME EXTENDS NAME '\{'
        ;

packet\_header
        : PACKET NAME '\{'
        | PACKET NAME EXTENDS NAME '\{'
        ;

class\_header
        : CLASS NAME '\{'
        | CLASS NAME EXTENDS NAME '\{'
        ;

struct\_header
        : STRUCT NAME '\{'
        | STRUCT NAME EXTENDS NAME '\{'
        ;

body
        : opt\_fields\_and\_properties
          opt\_propertiesblock\_old
          opt\_fieldsblock\_old
          '\}' opt\_semicolon
        ;

opt\_fields\_and\_properties
        : fields\_and\_properties
        |
        ;

fields\_and\_properties
        : fields\_and\_properties field
        | fields\_and\_properties property
        | field
        | property
        ;

field
        : fieldmodifiers fielddatatype NAME
           opt\_fieldvector opt\_fieldenum opt\_fieldvalue ';'
        | fieldmodifiers NAME
           opt\_fieldvector opt\_fieldenum opt\_fieldvalue ';'
        ;

fieldmodifiers
        : ABSTRACT
        | READONLY
        | ABSTRACT READONLY
        | READONLY ABSTRACT
        |
        ;

fielddatatype
        : NAME
        | NAME '*'
        | CHAR
        | SHORT
        | INT
        | LONG
        | UNSIGNED CHAR
        | UNSIGNED SHORT
        | UNSIGNED INT
        | UNSIGNED LONG
        | DOUBLE
        | STRING
        | BOOL
        ;

opt\_fieldvector
        : '[' INTCONSTANT ']'
        | '[' NAME ']'
        | '[' ']'
        |
        ;

opt\_fieldenum
        : ENUM '(' NAME ')'
        |
        ;

opt\_fieldvalue
        : '=' fieldvalue
        |
        ;

fieldvalue
        : fieldvalue fieldvalueitem
        | fieldvalueitem
        ;

fieldvalueitem
        : STRINGCONSTANT
        | CHARCONSTANT
        | INTCONSTANT
        | REALCONSTANT
        | TRUE
        | FALSE
        | NAME
        | '?' | ':' | '&&' | '||' | '##' | '==' | '!=' | '>' | '>=' | '<' | '<='
        | '&' | '|' | '#' | '<<' | '>>'
        | '+' | '-' | '*' | '/' | '%' | '^' | '&' | UMIN | '!' | '~'
        | '.' | ',' | '(' | ')' | '[' | ']'
        ;

enumvalue
        : INTCONSTANT
        | '-' INTCONSTANT
        | NAME
        ;

quantity
        : quantity INTCONSTANT NAME
        | quantity REALCONSTANT NAME
        | INTCONSTANT NAME
        | REALCONSTANT NAME
        ;

property
        : property\_namevalue ';'
        ;

property\_namevalue
        : property\_name
        | property\_name '(' opt\_property\_keys ')'
        ;

property\_name
        : '@' NAME
        | '@' NAME '[' NAME ']'
        ;

opt\_property\_keys
        : property\_keys
        ;

property\_keys
        : property\_keys ';' property\_key
        | property\_key
        ;

property\_key
        : NAME '=' property\_values
        | property\_values
        ;

property\_values
        : property\_values ',' property\_value
        | property\_value
        ;

property\_value
        : NAME
        | '$' NAME
        | STRINGCONSTANT
        | TRUE
        | FALSE
        | INTCONSTANT
        | REALCONSTANT
        | quantity
        | '-'
        |
        ;

opt\_fieldsblock\_old
        : FIELDS ':'
          opt\_fields\_old
        |
        ;

opt\_fields\_old
        : fields\_old
        |
        ;

fields\_old
        : fields\_old field
        | field
        ;

opt\_propertiesblock\_old
        : PROPERTIES ':'
          opt\_properties\_old
        |
        ;

opt\_properties\_old
        : properties\_old
        |
        ;

properties\_old
        : properties\_old property\_old
        | property\_old
        ;

property\_old
        : NAME '=' property\_value ';'
        ;

opt\_semicolon : ';' | ;

\end{Verbatim}


%%% Local Variables:
%%% mode: latex
%%% TeX-master: "usman"
%%% End:
